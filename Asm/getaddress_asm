BV_NTBAS        equ     $18
BV_NTP          equ     $1C
BV_NLBAS        equ     $20
BV_NLP          equ     $24
BV_RIP          equ     $58
error_bp        equ     -15
error_nf        equ     -1

start   lea     define,a1
        move.w  BP_INIT,a2
        jmp     (a2)

*function definition table

define  dc.w    0               No of procedures
        dc.w    0               end of procedures
        dc.w    1               no of functions
        dc.w    GetAddr-*
        dc.b    6,'GtAddr'    name of function
        dc.w    0               end of functions

*start of main function

GetAddr move.l  a5,d7           top of parameters
        sub.l   a3,d7
        divu    #8,d7           get no of parameters
        cmpi.w  #1,d7           should be 1
        beq.s   par_OK
        moveq   #error_bp,d0
        rts
par_ok  move.w  CA_GTSTR,a2     get string parameter
        jsr     (a2)
        tst.l   d0              did it work?
        beq.s   OK1
        rts
OK1     cmpi.w  #1,d3           check that we have 1 parameter
        beq.s   OK2
        moveq   #error_bp,d0    return with error
        rts

*at this point we have our one parameter on the maths stack
*copy the string to input_str:

OK2     lea     input_str,a3    destination address
        lea     0(a6,a1.l),a2   source address
        bsr     copy_str

*cleanup stack

        move.w  0(a6,a1.l),d0
        addq.w  #3,d0
        bclr    #0,d0
        adda.w  d0,a1

* the input string parameter is now copied into the string buffer.
* d7 contains offset in name table and name list

        clr.l   d7                      main loop counter
        clr.l   d2                      debugging
main_l  move.l  BV_NTBAS(a6),a4
        adda.l  d7,a4                   increase pointer in loop
        move.l  BV_NTP(a6),d6
        cmpa.l  d6,a4                   Have we reached the end of the name list?
        bcc.s   not_found
*       move.w  0(a6,a4.l),d2           type
        move.w  2(a6,a4.l),d3           name_pointer
        move.l  4(a6,a4.l),d4           value
        movea.l BV_NLBAS(a6),a4         get name list pointer
        adda.l  d3,a4                   add accumulated offset to pointer

*get name from name list into curr_name
        lea     curr_name,a3
        lea     0(a6,a4.l),a5
        bsr     copy_name

*The name is now in the name buffer

*compare the parameter string with the name string

        move.b  #error_nf,d0
        lea     curr_name,a0
        lea     input_str,a1
        bsr     comp_str
        tst.b   d0                      are the strings equal (d0=0)?
        beq.s   ret_val                 if so then return the value
        addq.l  #8,d7
        addq.l  #1,d2
        bra     main_l                  else go back and try again

not_found       move.l  d2,d4           return no of iterations if not found

ret_val lea     num,a1
        move.l  d4,(a1)                 get address and move it to num buffer
        lea     conv_str,a0             get address of string buffer
        bsr     conv_long
        move.l  #20,d1                  Allocate space on maths stack
        move.l  BV_RIP(a6),a1           Norm says do this before calling for allocation
        move.w  BV_CHRIX,a2             Reserve space on Maths stack
        jsr     (a2)
        lea     strlen,a2               put destination string buff on a2
        lea     0(a6,a1.l),a3           source string address on a3
        move.w  #8,(a2)                 move length word to beginning of source string
        bsr     copy_str
        moveq   #1,d4                   return type = string
        moveq   #0,d0                   and return
        rts

*Subroutines*

*compare two strings
comp_str        suba.l  a6,a0
                suba.l  a6,a1
                move.w  UT_CSTR,a2
                jsr     (a2)
                rts

*Convert long to hex
conv_long       movem.l a0/a1,-(a7)
                suba.l  a6,a0
                suba.l  a6,a1
                move.w  CN_ITOHL,a2
                jsr     (a2)
                movem.l (a7)+,a0/a1
                rts

*copy string
*destination buffer on a3
*source on a2
copy_str        clr.l   d5
                move.w  (a2),d5         get string length into a1
                move.w  d5,(a3)
                addq.l  #2,d5           get no of bytes to copy
cloop           move.b  (a2),(a3)+      copy byte
                addq.l  #1,a2           inc pointer
                subq.l  #1,d5           dec loop index
                tst.l   d5              is loop index 0?
                bne.s   cloop
                rts

* copy a name from the name list to a buffer
* destination buffer on a3
* source name list entry on a5
* d5 is loop counter
copy_name       move.b  (a5)+,d5                 get string length
                ext.w   d5
                move.w  d5,(a3)+
m_loop          move.b  (a5)+,(a3)+
                subq.l  #1,d5
                tst.l   d5
                bne.s   m_loop
                rts

*data*

num             dc.l    0
                dc.l    0,0,0,0,0,0,0,0
strlen          dc.w    0
conv_str        dc.l    0,0,0,0,0,0,0,0
curr_name       dc.w    0
name_chr        dc.l    0,0,0,0,0,0,0,0
input_str       dc.l    0,0,0,0,0,0,0,0
end
